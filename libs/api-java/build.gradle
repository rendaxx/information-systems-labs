plugins {
    id 'java-library'
    id 'org.openapi.generator' version '7.4.0'
    id 'io.spring.dependency-management'
}

description = 'Generated Java API surface for information-systems backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

def openApiOutputDir = "$buildDir/generated/openapi"

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/specs/openapi/openapi.yml"
    outputDir = openApiOutputDir
    apiPackage = 'com.rendaxx.labs.api.v1.api'
    modelPackage = 'com.rendaxx.labs.api.v1.model'
    globalProperties = [
            apis      : '',
            models    : '',
            modelDocs : 'false',
            modelTests: 'false',
            apiDocs   : 'false',
            supportingFiles: ''
    ]
    additionalProperties = [
            interfaceOnly       : 'true',
            useJakartaEe        : 'true',
            useTags             : 'true',
            useBeanValidation   : 'true',
            useResponseEntity   : 'true',
            apiNameSuffix       : 'Api',
            modelNameSuffix     : 'ApiDto',
            enumNameSuffix      : 'Api',
            dateLibrary         : 'java8',
            bigDecimalAsString  : 'false',
            generatedAnnotation : 'false',
            useLocalDateTime    : 'true'
    ]

    typeMappings = [
            DateTime: 'java.time.LocalDateTime'
    ]

    importMappings = [
            DateTime: 'java.time.LocalDateTime'
    ]
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

sourceSets {
    main {
        java {
            srcDir "${openApiOutputDir}/src/main/java"
        }
    }
}

configurations {
    openApi
}

dependencies {
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-context'
    api 'jakarta.validation:jakarta.validation-api'
    api 'jakarta.annotation:jakarta.annotation-api'
    api 'jakarta.servlet:jakarta.servlet-api'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    api 'org.openapitools:jackson-databind-nullable:0.2.6'
    api 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.21'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.6'
    }
}

tasks.register('cleanOpenApi') {
    group = LifecycleBasePlugin.CLEAN_TASK_NAME
    doLast {
        delete openApiOutputDir
    }
}

tasks.named('clean') {
    dependsOn tasks.named('cleanOpenApi')
}
