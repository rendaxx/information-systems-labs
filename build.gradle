plugins {
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
    id 'java-library'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

group = 'com.rendaxx'
version = '0.0.1-SNAPSHOT'
description = 'information-systems-labs'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    pluginManager.withPlugin('java') {
        apply plugin: 'com.diffplug.spotless'
        apply plugin: 'checkstyle'

        spotless {
            java {
                palantirJavaFormat('2.47.0')
                target 'src/*/java/**/*.java'
                targetExclude('build/**')
            }
        }

        checkstyle {
            toolVersion = '10.20.2'
            config = resources.text.fromFile(rootProject.file('config/checkstyle/checkstyle.xml'))
            configProperties = [
                'checkstyle.suppressions.file': rootProject.file('config/checkstyle/suppressions.xml').absolutePath
            ]
            maxWarnings = 0
        }

        tasks.withType(Checkstyle).configureEach {
            exclude('**/generated/**')
            exclude('**/build/**')
        }
        tasks.named('check') {
            dependsOn 'spotlessCheck'
        }
    }
}
